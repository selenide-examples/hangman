defaultTasks 'clean', 'test'

apply plugin: 'java'
apply plugin: 'war'

sourceSets {
  main {
    java {srcDir 'src'}
    resources {srcDir 'src'}
  }
  test {
    java {srcDir 'test'}
    resources {srcDir 'test'}
  }
}

repositories{
  mavenCentral()
}

[compileJava, compileTestJava]*.options.collect {options -> options.encoding = 'UTF-8'}
[compileJava, compileTestJava]*.options.collect {options -> options.debug = true}

sourceCompatibility = 1.8
targetCompatibility = 1.8

test {
  include 'ee/era/hangman/**'
}

task uitest_firefox(type: Test) {
  systemProperties['selenide.browser'] = 'firefox'
}

task uitest_edge(type: Test) {
  systemProperties['selenide.browser'] = 'edge'
}

task uitest_chrome(type: Test) {
  systemProperties['selenide.browser'] = 'chrome'
}

tasks.withType(Test) {
  systemProperties['file.encoding'] = 'UTF-8'
  System.properties.stringPropertyNames()
      .findAll { it.startsWith("selenide.") }
      .forEach {
        println " set ${it} to ${System.getProperty(it)}"
        systemProperties[it] = System.getProperty(it)
      }
  testLogging.showStandardStreams = true
  outputs.upToDateWhen { false }
}

dependencies {
  implementation 'org.eclipse.jetty:jetty-server:9.4.36.v20210114'
  implementation 'org.eclipse.jetty:jetty-webapp:9.4.36.v20210114'
  implementation 'org.eclipse.jetty:jetty-jsp:9.3.0.M1'
  implementation 'org.eclipse.jetty.toolchain:jetty-osgi-servlet-api:4.0.1'

  implementation 'org.apache.struts:struts2-core:2.3.37'
  implementation 'org.apache.struts.xwork:xwork-core:2.3.37'
  implementation 'org.apache.struts:struts2-convention-plugin:2.3.37'
  implementation 'org.apache.struts:struts2-json-plugin:2.3.37'
  implementation 'org.liquibase:liquibase-core:4.2.2'
  implementation('com.google.inject.extensions:guice-struts2:4.2.3') {
    exclude group: 'com.google.guava'
  }
  implementation 'com.google.guava:guava:31.1-jre'
  runtimeOnly 'com.h2database:h2:1.4.200'
  implementation 'c3p0:c3p0:0.9.1.2'
  testRuntimeOnly 'org.slf4j:slf4j-log4j12:2.0.3'
  testImplementation 'org.hamcrest:hamcrest-all:1.3'
  testImplementation('junit:junit:4.13.2') {transitive = false}
  testImplementation 'org.mockito:mockito-core:4.8.1'
  testImplementation 'com.codeborne:selenide:6.9.0'
}

task(run, dependsOn: 'compileJava', type: JavaExec) {
  main = 'ee.era.hangman.Launcher'
  classpath = sourceSets.main.runtimeClasspath
  systemProperty 'file.encoding', 'UTF-8'
}

war {
  from 'webapp'
}
