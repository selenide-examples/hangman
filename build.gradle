defaultTasks 'clean', 'test'

apply plugin: 'java'

sourceSets {
  main {
    java {srcDir 'src'}
    resources {srcDir 'src'}
  }
  test {
    java {srcDir 'test'}
    resources {srcDir 'test'}
  }
}

repositories{
  mavenCentral()
}

[compileJava, compileTestJava]*.options.collect {options -> options.encoding = 'UTF-8'}
[compileJava, compileTestJava]*.options.collect {options -> options.debug = true}

sourceCompatibility = 17
targetCompatibility = 17

test {
  include 'ee/era/hangman/**'
}

tasks.register('uitest_firefox', Test) {
  systemProperties['selenide.browser'] = 'firefox'
}

tasks.register('uitest_edge', Test) {
  systemProperties['selenide.browser'] = 'edge'
}

tasks.register('uitest_chrome', Test) {
  systemProperties['selenide.browser'] = 'chrome'
}

tasks.withType(Test).configureEach {
  systemProperties['file.encoding'] = 'UTF-8'
  System.properties.stringPropertyNames()
    .findAll { it.startsWith("selenide.") }
    .forEach {
      println " set ${it} to ${System.getProperty(it)}"
      systemProperties[it] = System.getProperty(it)
    }
  testLogging.showStandardStreams = true
  outputs.upToDateWhen { false }
}

dependencies {
  implementation 'com.fasterxml.jackson.core:jackson-databind:2.15.1'
  implementation('org.liquibase:liquibase-core:4.22.0') {
    exclude group: 'org.yaml'
  }
  implementation('commons-io:commons-io:2.12.0')
  implementation 'com.google.guava:guava:31.1-jre'
  runtimeOnly 'com.h2database:h2:2.1.214'
  implementation 'c3p0:c3p0:0.9.1.2'
  implementation 'org.slf4j:slf4j-api:2.0.7'
  runtimeOnly 'org.slf4j:slf4j-log4j12:2.0.7'
  testImplementation 'org.hamcrest:hamcrest-all:1.3'
  testImplementation('junit:junit:4.13.2') {transitive = false}
  testImplementation 'org.mockito:mockito-core:5.3.1'
  testImplementation 'com.codeborne:selenide:6.14.1'
}

tasks.register('run', JavaExec) {
  dependsOn 'compileJava'
  mainClass = 'ee.era.hangman.Launcher'
  classpath = sourceSets.main.runtimeClasspath
  systemProperty 'file.encoding', 'UTF-8'
}
